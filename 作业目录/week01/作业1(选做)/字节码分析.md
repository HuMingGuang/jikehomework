# 字节码具体分析
## 1.访问标志
```xml
public class Hello 
  minor version: 0
  major version: 52  
  flags: ACC_PUBLIC, ACC_SUPER
```
- public class Hello: 类名
- minor version: 0   小版本号
- major version: 52  主版本号
- flags: ACC_PUBLIC, ACC_SUPER 
    - ACC_PUBLIC:类或接口的访问权限为，包外可访问
    - ACC_SUPER:用于兼容早期编译器，新编译器都设置该标记，以在使用`invokespecial`指令时对子类方法做特定处理
## 2.常量池
### 2.1 存放方法引用
`#1 = Methodref          #21.#46        // java/lang/Object."<init>":()V`  
示例：表示指向Object类的init方法，`#21.#46`表示要跳转到第21个位置Obect类，然后引用第46个位置的init方法
### 2.2 基本类型常量
`#2 = Double             2.0d`  
示例：在常量池存放Doule类型的常量
### 3.方法
### 3.1<init>方法
```xml
public Hello();
    descriptor: ()V
    flags: ACC_PUBLIC
    Code:
      stack=1, locals=1, args_size=1
         0: aload_0
         1: invokespecial #1                  // Method java/lang/Object."<init>":()V
         4: return
      LineNumberTable:
        line 1: 0
      LocalVariableTable:
        Start  Length  Slot  Name   Signature
            0       5     0  this   LHello;
```
- `stack=1, locals=1, args_size=1`:操作数栈最大深度为1，局部变量最大槽数为1，声明变量数为1
- `0 aload_0`:从局部变量0中装在引用类型值入栈
- `1: invokespecial #1                  // Method java/lang/Object."<init>":()V`:调用常量池中位置1的方法，并将位置1中要跳转的两个常量入栈
- `4: return`: void函数返回

### 3.2 main方法
- `flags: ACC_PUBLIC, ACC_STATIC`:公共方法且为静态方法
- 整体分析：主要是三个内存空间的操作，变量池（取）、局部变量（取/放:store），操作栈(load:入栈局部变量，iconst:常量入栈)
```xml
 0 iconst_1                     // 1(int)入栈
 1 istore_1                     // 将栈顶int类型的值保存到局部变量1
 2 ldc2_w #2 <2.0>              // 常量池中2位置的double类型的常量入栈
 5 dstore_2                     // 将栈顶double类型的值保存到局部变量2
 6 ldc2_w #4 <3>                // 常量池中4位置的long类型的常量入栈
 9 lstore 4                     // 将栈顶long类型的值(4)保存到局部变量4
11 iconst_4                     // 4(int)入栈，从这里看出byte num4 = 4;是作为int类型来处理的
12 istore 6                     // 将栈顶int类型的值保存到局部变量6
14 ldc #6                       // 将常量池中6位置string类型的常量""入栈
16 invokevirtual #7 <java/lang/String.length>   //调用常量池中位置7的方法，即"".length() 
19 bipush 10                    // 将valuebyte值(10)带符号扩展成int(10)值入栈
21 if_icmpge 54 (+33)           // 若"".length() < 10)成立，则继续，否则跳转到栈中位置54, +33 即到栈中位置87 return方法
24 getstatic #8 <java/lang/System.out>  //调用常量池中位置8的打印方法
27 new #9 <java/lang/StringBuilder>     //创建StringBuilder对象实例
30 dup                          // 复制栈顶一个字长的数据，将复制后的数据压栈
31 invokespecial #10 <java/lang/StringBuilder.<init>>   // 调用StringBuilder初始化方法
34 ldc #11 <错误用法: num2 + num3 = >                     // 将字符串"错误用法: num2 + num3 = "入栈
36 invokevirtual #12 <java/lang/StringBuilder.append>   // 调用StringBuilder.append方法，append.("错误用法: num2 + num3 = ")
39 dload_2                                              // 将double类型的局部变量2即num2入栈
40 invokevirtual #13 <java/lang/StringBuilder.append>   // 调用StringBuilder.append方法，append.(2.0)
43 lload 4                                              // 从局部变量4中装载long类型值入栈
45 invokevirtual #14 <java/lang/StringBuilder.append>   // 调用StringBuilder.append方法,append.(3)
48 invokevirtual #15 <java/lang/StringBuilder.toString> // 调用StringBuilder.toString()方法，结果为"错误用法: num2 + num3 = 2.03"
51 invokevirtual #16 <java/io/PrintStream.println>      // 调用PrintStream.println方法，打印出上述结果
54 iconst_0                                             // 将int值(0)入栈
55 istore 7                                             // 将int类型的值放入局部变量7 
57 iload 7                                              // 将局部变量入7栈
59 iload_1                                              // 将局部变量1入栈
60 if_icmpge 87 (+27)                                   // 若i < num1成立，则继续，否则跳转到栈位置87，即return
63 getstatic #8 <java/lang/System.out>                  // 调用静态方法System.out
66 ldc #17 <四则运算: num1 * num4 = >                     // 将字符串"四则运算: num1 * num4 = "入栈 
68 invokevirtual #18 <java/io/PrintStream.print>        // 调用PrintStream方法
71 getstatic #8 <java/lang/System.out>                  // 获取常量池中位置8的System.out静态类的值
74 iload_1                                              // 将局部变量1入栈
75 iload 6                                              // 将局部变量6入栈
77 imul                                                 // 将栈顶两个int类型数相乘
78 invokevirtual #19 <java/io/PrintStream.println>      // 调用PrintStream.println方法打印出结果
81 iinc 7 by 1                                          // 将局部变量7加1，即i++
84 goto 57 (-27)                                        // 跳转到栈位置57
87 return

```